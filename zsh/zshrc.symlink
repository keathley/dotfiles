PROFILE_STARTUP=false
if [[ "$PROFILE_STARTUP" == true ]]; then
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    PS4=$'%D{%M%S%.} %N:%i> '
    exec 3>&2 2>$HOME/tmp/startlog.$$
    setopt xtrace prompt_subst
fi

stty -ixon

# Path to your zsh installation.
export ZSH=$HOME/dotfiles

export PROJECTS=~/Development

if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# zsh files
typeset -U config_files
config_files=($ZSH/(**)/*(.)zsh)

for file in ${(M)config_files:#*/_path.zsh}; do
  source $file
done

for file in ${${config_files:#*/_path.zsh}:#*/completion.zsh}; do
  source $file
done

autoload -Uz compinit
compinit -i

for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

unset config_files

if [[ "$PROFILE_STARTUP" == true ]]; then
    unsetopt xtrace
    exec 2>&3 3>&-
fi
